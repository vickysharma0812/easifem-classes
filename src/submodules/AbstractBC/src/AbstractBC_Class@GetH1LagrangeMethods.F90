! This program is a part of EASIFEM library
! Copyright (C) 2020-2021  Vikas Sharma, Ph.D
!
! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <https: //www.gnu.org/licenses/>

SUBMODULE(AbstractBC_Class) GetH1LagrangeMethods
USE Display_Method, ONLY: ToString

USE GlobalData, ONLY: CHAR_LF

USE AbstractMesh_Class, ONLY: AbstractMesh_

IMPLICIT NONE
CONTAINS

!----------------------------------------------------------------------------
!                                                           GetTotalNodeNum
!----------------------------------------------------------------------------

MODULE PROCEDURE obj_H1_Lagrange_GetTotalNodenum
ans = obj%boundary%GetTotalNodeNum(obj%dom)
END PROCEDURE obj_H1_Lagrange_GetTotalNodenum

!----------------------------------------------------------------------------
!                                                             CheckError
!----------------------------------------------------------------------------

SUBROUTINE checkerror(obj, myName)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  CHARACTER(*), INTENT(IN) :: myName

  LOGICAL(LGT) :: isok

  CALL AssertError1(obj%isInitiated, myName, &
                    'AbstractBC_ object is not initiated, initiate it first.')

  isok = ASSOCIATED(obj%dom)
  CALL AssertError1(isok, myName, &
                    'AbstractBC_::obj%dom is not associated!')
END SUBROUTINE checkerror

!----------------------------------------------------------------------------
!                                                                       Get
!----------------------------------------------------------------------------

MODULE PROCEDURE obj_Get_H1_Lagrange1
#ifdef DEBUG_VER
CHARACTER(*), PARAMETER :: myName = "obj_Get_H1_Lagrange1()"
#endif

#ifdef DEBUG_VER
CALL e%RaiseInformation(modName//'::'//myName//' - '// &
                        '[START]')
#endif

#ifdef DEBUG_VER
CALL checkerror(obj, myName)

IF (obj%isUserFunction) THEN
  CALL checkerror_uf(obj, myName, times)
END IF

#endif

! get nodal values
SELECT CASE (obj%nodalValueType)

! Constant
CASE (TypeFEVariableOpt%constant)

  IF (obj%isUserFunction) THEN
    CALL GetConstantValue_uf(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  ELSE
    CALL GetConstantValue(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  END IF

! Space
CASE (TypeFEVariableOpt%space)

  IF (obj%isUserFunction) THEN
    CALL GetSpaceValue_uf(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  ELSE
    CALL GetSpaceValue(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  END IF

! Time
CASE (TypeFEVariableOpt%time)

  IF (obj%isUserFunction) THEN
    CALL GetTimeValue_uf(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  ELSE
    CALL GetTimeValue(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  END IF

! Space-Time
CASE (TypeFEVariableOpt%spacetime)

  IF (obj%isUserFunction) THEN
    CALL GetSpaceTimeValue_uf(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  ELSE
    CALL GetSpaceTimeValue(obj=obj, fedof=fedof, nodeNum=nodeNum, &
                     nodalValue=nodalValue, nrow=nrow, ncol=ncol, times=times)
  END IF

END SELECT

#ifdef DEBUG_VER
CALL e%RaiseInformation(modName//'::'//myName//' - '// &
                        '[END]')
#endif
END PROCEDURE obj_Get_H1_Lagrange1

!----------------------------------------------------------------------------
!                                                             GetH1Lagrange
!----------------------------------------------------------------------------

MODULE PROCEDURE obj_Get_H1_Lagrange2
#ifdef DEBUG_VER
CHARACTER(*), PARAMETER :: myName = "obj_Get_H1_Lagrange2()"
#endif

#ifdef DEBUG_VER
CALL e%RaiseInformation(modName//'::'//myName//' - '// &
                        '[START]')
#endif

#ifdef DEBUG_VER
CALL checkerror(obj, myName)
#endif

CALL GetNodeNum(obj=obj, fedof=fedof, nodenum=nodenum, nrow=tsize)

#ifdef DEBUG_VER
CALL e%RaiseInformation(modName//'::'//myName//' - '// &
                        '[END] ')
#endif
END PROCEDURE obj_Get_H1_Lagrange2

!----------------------------------------------------------------------------
!                                                                GetNodeNum
!----------------------------------------------------------------------------

SUBROUTINE GetNodeNum(obj, fedof, nodenum, nrow)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! Boundary condition
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! Degree of freedom
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! Size of nodeNum can be obtained from obj%boundary%GetTotalNodeNum
  INTEGER(I4B), INTENT(OUT) :: nrow
  !! the size of data written in nodalValue

  CHARACTER(*), PARAMETER :: myName = "GetNodeNum()"
  INTEGER(I4B) :: nsd
  CLASS(AbstractMesh_), POINTER :: mesh

  CALL obj%boundary%GetNodeNum(dom=obj%dom, ans=nodenum, tsize=nrow)
  IF (nrow .NE. 0) THEN
    nsd = obj%dom%GetNSD()
    mesh => obj%dom%GetMeshPointer(dim=nsd)
    CALL mesh%GetLocalNodeNumber_(globalNode=nodenum, ans=nodenum, &
                                  islocal=.FALSE.)
    mesh => NULL()
  END IF
END SUBROUTINE GetNodeNum

!----------------------------------------------------------------------------
!                                                          GetConstantValue
!----------------------------------------------------------------------------

SUBROUTINE GetConstantValue(obj, fedof, nodeNum, nodalValue, nrow, ncol, &
                            times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! Boundary condition
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! Degree of freedom
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! Size of nodeNum can be obtained from obj%boundary%GetTotalNodeNum
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nrow = Size of nodeNum
  !! ncol = 1 or Size of times
  INTEGER(I4B), INTENT(OUT) :: nrow, ncol
  !! the size of data written in nodalValue
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! times vector is only used when usefunction is true in obj

  !! internal variables
  CHARACTER(*), PARAMETER :: myName = "GetConstantValue()"
  LOGICAL(LGT) :: isok
  INTEGER(I4B) :: ii, jj

#ifdef DEBUG_VER
  isok = ALLOCATED(obj%nodalValue)
  CALL AssertError1(isok, myname, &
                    'AbstractBC_::obj%nodalValue is not allocated!')
#endif

  CALL GetNodeNum(obj=obj, fedof=fedof, nodenum=nodeNum, nrow=nrow)

  ncol = 1
  IF (PRESENT(times)) ncol = SIZE(times)

  DO CONCURRENT(ii=1:nrow, jj=1:ncol)
    nodalValue(ii, jj) = obj%nodalValue(1, 1)
  END DO
END SUBROUTINE GetConstantValue

!----------------------------------------------------------------------------
!                                                        GetConsantValue_uf
!----------------------------------------------------------------------------

SUBROUTINE GetConstantValue_uf(obj, fedof, nodeNum, nodalValue, nrow, ncol, &
                               times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! Abstract boundary condition
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! node number should be allocated
  !! size of nodenum is nrow
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nodal values
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! time value
  !! time values are needed when userfunction is time or space-time
  INTEGER(I4B) :: nrow, ncol
  !! nrow is size of nodenum and size of number of rows in nodalvalue
  !! ncol is colsize of nodalvalue

  !! internal variables
#ifdef DEBUG_VER
  CHARACTER(*), PARAMETER :: myName = "GetConstantValue_uf()"
#endif

  REAL(DFP) :: ans
  INTEGER(I4B) :: ii, jj

#ifdef DEBUG_VER
  CALL e%RaiseInformation(modName//'::'//myName//' - '// &
                          '[START] ')
#endif

  CALL obj%func%Get(val=ans)

  nrow = 0; ncol = 1
  IF (PRESENT(times)) ncol = SIZE(times)

  CALL GetNodeNum(obj=obj, fedof=fedof, nodenum=nodenum, nrow=nrow)

  DO CONCURRENT(ii=1:nrow, jj=1:ncol)
    nodalValue(ii, jj) = ans
  END DO

#ifdef DEBUG_VER
  CALL e%RaiseInformation(modName//'::'//myName//' - '// &
                          '[END] ')
#endif

END SUBROUTINE GetConstantValue_uf

!----------------------------------------------------------------------------
!                                                             GetSpaceValue
!----------------------------------------------------------------------------

SUBROUTINE GetSpaceValue(obj, fedof, nodeNum, nodalValue, nrow, ncol, &
                         times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! Boundary condition
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! Degree of freedom
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! Size of nodeNum can be obtained from obj%boundary%GetTotalNodeNum
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nrow = Size of nodeNum
  !! ncol = 1 or Size of times
  INTEGER(I4B), INTENT(OUT) :: nrow, ncol
  !! the size of data written in nodalValue
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! times vector is only used when usefunction is true in obj

  !! internal variables
  CHARACTER(*), PARAMETER :: myName = "GetSpaceValue()"
  LOGICAL(LGT) :: isok
  INTEGER(I4B) :: ii, jj

#ifdef DEBUG_VER
  isok = ALLOCATED(obj%nodalValue)
  CALL AssertError1(isok, myname, &
                    'AbstractBC_::obj%nodalValue is not allocated!')
#endif

  CALL GetNodeNum(obj=obj, fedof=fedof, nodenum=nodeNum, nrow=nrow)

#ifdef DEBUG_VER
  isok = obj%nrow .GE. nrow
  CALL AssertError1(isok, myname, &
                    'SIZE( obj%nodalValue, 1 ) .NE. SIZE( nodeNum )')
#endif

  ncol = 1
  IF (PRESENT(times)) ncol = SIZE(times)

  DO CONCURRENT(ii=1:nrow, jj=1:ncol)
    nodalValue(ii, jj) = obj%nodalValue(ii, 1)
  END DO

END SUBROUTINE GetSpaceValue

!----------------------------------------------------------------------------
!                                                          GetSpaceValue_uf
!----------------------------------------------------------------------------

SUBROUTINE GetSpaceValue_uf(obj, fedof, nodeNum, nodalValue, nrow, ncol, &
                            times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !!
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! fedof
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! node number should be allocated
  !! size of nodenum is nrow
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nodal values
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! time value
  !! time values are needed when userfunction is time or space-time
  INTEGER(I4B) :: nrow, ncol
  !! nrow is size of nodenum and size of number of rows in nodalvalue
  !! ncol is colsize of nodalvalue

  !! internal variables
  REAL(DFP) :: ans, xij(4, 1)
  INTEGER(I4B) :: ii, jj, nsd, tnodes, tsize
  CLASS(AbstractMesh_), POINTER :: meshptr

  nsd = obj%dom%GetNSD()
  meshptr => obj%dom%GetMeshPointer(dim=nsd)
  nrow = SIZE(nodeNum); ncol = 1
  IF (PRESENT(times)) ncol = SIZE(times)

  DO ii = 1, tnodes
    CALL meshptr%GetNodeCoord(nodeCoord=xij(:, 1), tsize=tsize, &
                              globalNode=nodeNum(ii), islocal=.TRUE.)

    CALL obj%func%Get(val=ans, args=xij(1:3, 1))

    DO jj = 1, ncol
      nodalValue(ii, 1) = ans
    END DO
  END DO

  meshptr => NULL()

END SUBROUTINE GetSpaceValue_uf

!----------------------------------------------------------------------------
!                                                             GetTimeValue
!----------------------------------------------------------------------------

SUBROUTINE GetTimeValue(obj, fedof, nodeNum, nodalValue, nrow, ncol, &
                        times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! Boundary condition
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! Degree of freedom
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! Size of nodeNum can be obtained from obj%boundary%GetTotalNodeNum
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nrow = Size of nodeNum
  !! ncol = 1 or Size of times
  INTEGER(I4B), INTENT(OUT) :: nrow, ncol
  !! the size of data written in nodalValue
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! times vector is only used when usefunction is true in obj

  !! internal variables
  CHARACTER(*), PARAMETER :: myname = 'GetTimeValue()'
  INTEGER(I4B) :: ii, jj
  LOGICAL(LGT) :: isok

#ifdef DEBUG_VER
  isok = ALLOCATED(obj%nodalValue)
  CALL AssertError1(isok, myname, &
                    'AbstractBC_::obj%nodalValue is not allocated!')
#endif

  CALL GetNodeNum(obj=obj, fedof=fedof, nodenum=nodeNum, nrow=nrow)

  ncol = obj%nrow

  DO CONCURRENT(ii=1:nrow, jj=1:ncol)
    nodalValue(ii, jj) = obj%nodalValue(jj, 1)
  END DO
END SUBROUTINE GetTimeValue

!----------------------------------------------------------------------------
!                                                           GetTimeValue_uf
!----------------------------------------------------------------------------

SUBROUTINE GetTimeValue_uf(obj, fedof, nodeNum, nodalValue, nrow, ncol, times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! obj
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! fedof
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! node number should be allocated
  !! size of nodenum is nrow
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nodal values
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! time value
  !! time values are needed when userfunction is time or space-time
  INTEGER(I4B) :: nrow, ncol
  !! nrow is size of nodenum and size of number of rows in nodalvalue
  !! ncol is colsize of nodalvalue

  REAL(DFP) :: ans
  INTEGER(I4B) :: ii, jj

  ncol = SIZE(times)
  nrow = SIZE(nodeNum)

  DO jj = 1, ncol
    CALL obj%func%Get(val=ans, args=times(jj:jj))
    DO ii = 1, nrow
      nodalValue(ii, jj) = ans
    END DO
  END DO

END SUBROUTINE GetTimeValue_uf

!----------------------------------------------------------------------------
!                                                         GetSpaceTimeValue
!----------------------------------------------------------------------------

SUBROUTINE GetSpaceTimeValue(obj, fedof, nodeNum, nodalValue, nrow, ncol, &
                             times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! Boundary condition
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! Degree of freedom
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! Size of nodeNum can be obtained from obj%boundary%GetTotalNodeNum
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nrow = Size of nodeNum
  !! ncol = 1 or Size of times
  INTEGER(I4B), INTENT(OUT) :: nrow, ncol
  !! the size of data written in nodalValue
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! times vector is only used when usefunction is true in obj

  !! internal variables
  CHARACTER(*), PARAMETER :: myName = 'GetSpaceTimeValue()'
  LOGICAL(LGT) :: isok
  INTEGER(I4B) :: ii, jj

#ifdef DEBUG_VER
  isok = ALLOCATED(obj%nodalValue)
  CALL AssertError1(isok, myname, &
                    'AbstractBC_::obj%nodalValue is not allocated!')

#endif

  CALL GetNodeNum(obj=obj, fedof=fedof, nodenum=nodeNum, nrow=nrow)

#ifdef DEBUG_VER
  isok = obj%nrow .GE. nrow
  CALL AssertError1(isok, myname, &
                    'SIZE(obj%nodalValue, 1) .NE. SIZE(nodeNum)')
#endif

  ncol = obj%ncol

  DO CONCURRENT(ii=1:nrow, jj=1:ncol)
    nodalValue(ii, jj) = obj%nodalValue(ii, jj)
  END DO
END SUBROUTINE GetSpaceTimeValue

!----------------------------------------------------------------------------
!                                                       GetSpaceTimeValue_uf
!----------------------------------------------------------------------------

SUBROUTINE GetSpaceTimeValue_uf(obj, fedof, nodeNum, nodalValue, nrow, ncol, &
                                times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  !! obj
  CLASS(FEDOF_), INTENT(INOUT) :: fedof
  !! fedof
  INTEGER(I4B), INTENT(INOUT) :: nodeNum(:)
  !! node number should be allocated
  !! size of nodenum is nrow
  REAL(DFP), INTENT(INOUT) :: nodalValue(:, :)
  !! nodal values
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)
  !! time value
  !! time values are needed when userfunction is time or space-time
  INTEGER(I4B) :: nrow, ncol
  !! nrow is size of nodenum and size of number of rows in nodalvalue
  !! ncol is colsize of nodalvalue

  INTEGER(I4B) :: ii, jj, nsd, tsize
  REAL(DFP) :: xij(4, 1), ans
  CLASS(AbstractMesh_), POINTER :: meshptr

  ncol = SIZE(times)
  nrow = SIZE(nodeNum)
  nsd = obj%dom%GetNSD()
  meshptr => obj%dom%GetMeshPointer(dim=nsd)

  DO jj = 1, ncol
    xij(nsd + 1, 1) = times(jj)

    DO ii = 1, nrow
      CALL meshptr%GetNodeCoord(nodeCoord=xij(:, 1), tsize=tsize, &
                                globalNode=nodeNum(ii), islocal=.TRUE.)

      CALL obj%func%Get(val=ans, args=xij(1:4, 1))

      nodalValue(ii, jj) = ans
    END DO

  END DO

  meshptr => NULL()

END SUBROUTINE GetSpaceTimeValue_uf

!----------------------------------------------------------------------------
!                                                             checkerror_uf
!----------------------------------------------------------------------------

SUBROUTINE checkerror_uf(obj, myName, times)
  CLASS(AbstractBC_), INTENT(INOUT) :: obj
  CHARACTER(*), INTENT(IN) :: myName
  REAL(DFP), OPTIONAL, INTENT(IN) :: times(:)

  ! internal variable
  LOGICAL(LGT) :: problem, no_times
  INTEGER(I4B) :: aint

  no_times = .NOT. PRESENT(times)

  problem = .NOT. ASSOCIATED(obj%func)
  IF (problem) THEN
    CALL e%RaiseError(modName//'::'//myName//" - "// &
                      "[INTERNAL ERROR] :: When nodalValueType is "// &
                      CHAR_LF//"Space and useFunction is specified, "// &
                      CHAR_LF//"then SpaceFunction is needed, "// &
                      CHAR_LF//"but it is not associated")
    RETURN
  END IF

  aint = obj%func%GetReturnType()

  problem = aint .NE. TypeFEVariableOpt%scalar
  IF (problem) THEN
    CALL e%RaiseError(modName//'::'//myName//' - '// &
             '[INTERNAL ERROR] :: Return type of user function should be '// &
                      'scalar.')
    RETURN
  END IF

  problem = (obj%nodalValueType .EQ. TypeFEVariableOpt%time) .AND. no_times
  IF (problem) THEN
    CALL e%RaiseError(modName//'::'//myName//" - "// &
                      "[INTERNAL ERROR] :: When `nodalValueType` is Time "// &
                      " and `IsUserFunction` is TRUE, "// &
                      " then `times` is needed in the passing argument,"// &
                      " but it is not present")
    RETURN
  END IF

  problem = (obj%nodalValueType .EQ. TypeFEVariableOpt%spacetime) .AND. &
            no_times
  IF (problem) THEN
    CALL e%RaiseError(modName//'::'//myName//" - "// &
                 "[INTERNAL ERROR] :: When `nodalValueType` is SpaceTime "// &
                      " and `IsUserFunction` is TRUE, "// &
                      " then `times` is needed in the passing argument,"// &
                      " but it is not present")
    RETURN
  END IF

  aint = obj%func%GetArgType()

  problem = aint .NE. obj%nodalValueType
  IF (problem) THEN
    CALL e%RaiseError(modName//'::'//myName//' - '// &
                      '[INTERNAL ERROR] :: argType='//tostring(aint)// &
                      ' in user function is not same '// &
                      'as nodalValueType '//tostring(obj%nodalValueType)// &
                      ' in AbstractBC_')
    RETURN
  END IF

END SUBROUTINE checkerror_uf

!----------------------------------------------------------------------------
!
!----------------------------------------------------------------------------

#include "../../include/errors.F90"

END SUBMODULE GetH1LagrangeMethods
